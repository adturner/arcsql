Resources
| where type == 'microsoft.hybridcompute/machines'
| project
	id,
	JoinID = toupper(id),
	ComputerName = tostring(properties.osProfile.computerName),
    subscriptionId = tostring(subscriptionId),
	OSName = tostring(properties.osName)
| join kind=inner(
	Resources
	| where type == 'microsoft.hybridcompute/machines/extensions'
    | where properties.type in ('WindowsAgent.SqlServer','LinuxAgent.SqlServer')
    | parse properties with * 'uploadStatus : ' uploadStatus ';' *
	| project
		MachineId = toupper(substring(id, 0, indexof(id, '/extensions'))),
		ExtensionName = name,
        ExtensionStatus = properties.provisioningState,
        UploadStatus = uploadStatus,
        LicenseType = properties.settings.LicenseType
    //| where UploadStatus !in('OK')
) on $left.JoinID == $right.MachineId
| summarize Extensions = make_list(ExtensionName) by id, subscriptionId, ComputerName, OSName, tostring(ExtensionStatus), tostring(UploadStatus), tostring(LicenseType)
| order by subscriptionId, ExtensionStatus, UploadStatus, LicenseType, ComputerName asc